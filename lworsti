#!/bin/sh
# profiling: -jp -jp=s -jp=l2 -jp=a
# -jp=6 = 6 stack context, -jp=l6 = with line numbers
exec env "LUA_PATH=${LUA_PATH};${WORST_LIBDIR-./lib}/lua/lworst/?.lua" luajit -jp=l2 -- ${WORST_LIBDIR-./lib}/lua/lworst.lua $0 $@
!#

[ quote quote quote uplevel uplevel ] quote upquote definition-add

[ ; define name [def...]
    upquote upquote ; name [def ...]
    swap quote definition-add uplevel
] quote define definition-add

define const [
    [quote] swap list-push list-reverse
    upquote
    quote definition-add uplevel
]

; define builtin-quote [ quote quote uplevel ]

define ->string [ to-string/terse swap drop ]

define port-write-value [ ->string port-write-string ]

; bool if [if-true] [if-false]
define if [
    upquote upquote
    ; cond true false => false true cond
    swap dig
    quote swap when drop
    eval
]

; TODO add syntax-read here
; it's in worst.rkt but not lworst.lua because lworst never expected
; to be given anything but a source file to read, therefore it just reads this
; TODO also simplify syntax-read? port-read-value can be overridden instead
; and syntax-read is then just [ source-input-port port-read-value swap drop ]

define syntax-read [ source-input-port port-read-value swap drop ]

; path read-file -> list
define read-file [
    open-input-file
    define read-all [
        ; tail-call
        swap
        port-read-value eof-object? if [
            drop drop list-reverse
        ] [
            dig swap list-push
            read-all
        ]
    ]
    [] read-all
]

"WORST_LIBDIR" env-get swap drop
"./lib" swap or bury drop drop
const WORST_LIBDIR

; module-name resolve-import-path
; uses WORST_LIBDIR
define resolve-import-path [
    symbol? if [
        ->string
        WORST_LIBDIR "/" string-append
        swap string-append
        ".w" string-append
    ] [ "resolve-import-path: invalid" abort ]
]

; Very basic import/export
define import [
    upquote resolve-import-path
    read-file quote eval uplevel
]
define export-name [
    upquote
    definition-resolve
    swap
    quote definition-add quote uplevel uplevel
]

; Generally useful utilities
import worst/base

; Real import/export
import worst/module

; TODO fix: without this, documentation attribute does nothing, for e.g. help
import syntax/attribute

; Interactive
import doc
import ui

worst-repl

; vi: ft=scheme

